apply plugin: 'com.android.application'
apply plugin: 'androidx.navigation.safeargs'

android {

    compileSdkVersion 30

    def APPLICATION_ID = "org.blagodarie"

    def CONTENT_PROVIDER_AUTHORITIES = ".content.provider"
    def FILE_PROVIDER_AUTHORITIES = ".file.provider"

    defaultConfig {
        applicationId APPLICATION_ID
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 54
        versionName "0.4.17"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        project.ext.set("archivesBaseName", "health-" + defaultConfig.versionName);
        //multiDexEnabled true
    }

    buildTypes {

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            resValue "string", "content_provider_authorities", "${APPLICATION_ID}${CONTENT_PROVIDER_AUTHORITIES}"
            resValue "string", "file_provider_authorities", "${APPLICATION_ID}${FILE_PROVIDER_AUTHORITIES}"
        }
        debug {
            debuggable true

            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            applicationIdSuffix ".dbg"
            resValue "string", "content_provider_authorities", "${APPLICATION_ID}${applicationIdSuffix}${CONTENT_PROVIDER_AUTHORITIES}"
            resValue "string", "file_provider_authorities", "${APPLICATION_ID}${applicationIdSuffix}${FILE_PROVIDER_AUTHORITIES}"
        }
    }

    applicationVariants.all { variant ->
        /*variant.outputs.all {
            outputFileName = "health-" + versionName + "-" + buildType.name + ".apk"
        }*/
        variant.getAssembleProvider().configure() {
            it.doLast {
                variant.outputs.all {
                    println("copy apk to latest")
                    println("${projectDir}\\${buildType.name}\\output-metadata.json")

                    def distDir = "/latest"

                    def dbgSuffix = ""
                    if (buildType.name == "debug") {
                        dbgSuffix = "-dbg"
                    }

                    //скопировать apk в latest
                    copy {
                        from("${projectDir}\\${buildType.name}\\${outputFileName}") {
                            rename '.*', "health${dbgSuffix}-latest.apk"
                        }
                        into "${distDir}${dbgSuffix}"
                    }

                    //скопировать output.json в latest
                    copy {
                        from("${projectDir}\\${buildType.name}\\output-metadata.json") {
                            rename '.*', "output.json"
                        }
                        into "${distDir}${dbgSuffix}"
                    }

                }
            }
        }
    }

    dataBinding {
        enabled = true
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(path: ':authentication')
    implementation project(path: ':server')
    implementation project(path: ':database')
    implementation project(path: ':log')

    implementation "android.arch.paging:runtime:1.0.1"
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.constraintlayout:constraintlayout:2.0.1"
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation "androidx.navigation:navigation-dynamic-features-fragment:2.3.0"
    implementation "androidx.navigation:navigation-fragment:2.3.0"
    implementation "androidx.room:room-runtime:2.2.5"
    implementation 'com.google.android.material:material:1.3.0-alpha02'

    //implementation 'com.android.support:multidex:1.0.3'

    implementation "io.reactivex.rxjava2:rxjava:2.2.9"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.0"

    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    implementation 'com.google.android.material:material:1.2.1'
}