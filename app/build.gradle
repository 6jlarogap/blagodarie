apply plugin: 'com.android.application'

android {

    compileSdkVersion 28

    def APPLICATION_ID = "org.blagodarie"

    defaultConfig {
        applicationId APPLICATION_ID
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 27
        versionName "0.3.4"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }

    buildTypes {
        def CONTENT_PROVIDER_AUTHORITIES = ".content.provider"
        def FILE_PROVIDER_AUTHORITIES = ".file.provider"
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "content_provider_authorities", "${APPLICATION_ID}${CONTENT_PROVIDER_AUTHORITIES}"
            resValue "string", "file_provider_authorities", "${APPLICATION_ID}${FILE_PROVIDER_AUTHORITIES}"
        }
        latest {
            initWith release
        }
        debug {
            debuggable true
            applicationIdSuffix ".dbg"
            versionNameSuffix "-dbg"
            resValue "string", "content_provider_authorities", "${APPLICATION_ID}${applicationIdSuffix}${CONTENT_PROVIDER_AUTHORITIES}"
            resValue "string", "file_provider_authorities", "${APPLICATION_ID}${applicationIdSuffix}${FILE_PROVIDER_AUTHORITIES}"
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            if (buildType.name == "latest") {
                outputFileName = "../latest/blagodarie-" + buildType.name + ".apk"
            } else {
                outputFileName = "blagodarie-" + versionName + "-" + buildType.name + ".apk"
            }
        }
    }

    dataBinding {
        enabled = true
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(path: ':authentication')
    implementation project(path: ':server')
    implementation project(path: ':database')

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.constraintlayout:constraintlayout:2.0.0-beta4"
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation "androidx.navigation:navigation-dynamic-features-fragment:2.3.0-alpha05"
    implementation "androidx.navigation:navigation-fragment:2.3.0-alpha05"
    implementation "androidx.room:room-runtime:2.2.5"

    implementation 'com.android.support:multidex:1.0.3'

    //Не увеличивать! Уменьшено для поддержки Android < 5.0
    implementation 'com.squareup.okhttp3:okhttp:3.12.1'

    implementation "io.reactivex.rxjava2:rxjava:2.2.9"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.0"

    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation 'com.google.android.material:material:1.1.0'
}
